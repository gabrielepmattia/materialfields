// Library top buildscript
buildscript {
    ext.kotlin_version = '1.2.71'
    ext.dokka_version = '0.9.16'
    repositories {
        jcenter()
        google()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

// Library build script
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply from: 'versioning.gradle'

publishing {
    publications {
        Library(MavenPublication) {
            artifact("$buildDir/outputs/aar/materialfields-release.aar")
            groupId = 'com.gabrielepmattia.me'
            artifactId = 'materialfields'
            version = materialfieldsVersionName

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

def getVersionName() throws GradleException {
    if (project.hasProperty('publishTrack') && publishTrack == 'production')
        return materialfieldsVersionName
    else return materialfieldsVersionName + '-beta'
}

bintray {
    user bintrayUser
    key bintrayApiKey
    override = true
    pkg {
        repo = 'maven'
        name = 'materialfields'
        userOrg = bintrayUser
        licenses = ['LGPL-3.0']
        issueTrackerUrl = 'https://gitlab.com/gabrielepmattia/materialfields/issues'
        desc = 'Android library for creating awesome form fields'
        vcsUrl = 'https://gitlab.com/gabrielepmattia/materialfields.git'
        publish = true
        publicDownloadNumbers = true
        version {
            name = getVersionName()
            vcsTag = getVersionName()
            released = new Date()
        }
    }
    publications = ['Library']
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode materialfieldsVersionCode.toInteger()
        versionName materialfieldsVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    ext.support_lib_version = '28.0.0-rc02'

    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support:recyclerview-v7:$support_lib_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}
repositories {
    mavenCentral()
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includeNonPublic = false

    packageOptions {
        prefix = "android" // will match kotlin.internal and all sub-packages of it
        suppress = true
    }
}