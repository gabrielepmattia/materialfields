// Library top buildscript
buildscript {
    ext.kotlin_version = '1.3.21'
    ext.dokka_version = '0.9.16'
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

// Library build script
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply from: 'versioning.gradle'

publishing {
    publications {
        Library(MavenPublication) {
            artifact("$buildDir/outputs/aar/materialfields-release.aar")
            groupId = 'com.gabrielepmattia.me'
            artifactId = 'materialfields'
            version = getVersionName()

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

def getVersionName() throws GradleException {
    if (project.hasProperty('publishTrack') && publishTrack == 'production')
        return materialfieldsVersionName
    else return materialfieldsVersionName + '-beta'
}

def getVcsTag() throws GradleException {
    if (project.hasProperty('publishTrack') && publishTrack == 'production')
        return 'v' + materialfieldsVersionName
    else return 'v' + materialfieldsVersionName + 'b'
}

bintray {
    user bintrayUser
    key bintrayApiKey
    override = true
    pkg {
        repo = 'maven'
        name = 'materialfields'
        userOrg = bintrayUser
        licenses = ['LGPL-3.0']
        issueTrackerUrl = 'https://gitlab.com/gabrielepmattia/materialfields/issues'
        desc = 'Android library for creating awesome form fields'
        vcsUrl = 'https://gitlab.com/gabrielepmattia/materialfields.git'
        publish = true
        publicDownloadNumbers = true
        version {
            name = getVersionName()
            vcsTag = getVcsTag()
            released = new Date()
        }
    }
    publications = ['Library']
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode materialfieldsVersionCode.toInteger()
        versionName materialfieldsVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    ext.support_lib_version = '28.0.0-rc02'

    implementation 'androidx.appcompat:appcompat:1.1.0-alpha03'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha03'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-alpha02'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
}
repositories {
    mavenCentral()
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includeNonPublic = false

    packageOptions {
        prefix = "android" // will match kotlin.internal and all sub-packages of it
        suppress = true
    }
}

/*
task wrapper(type: Wrapper) {
    gradleVersion = '4.10.1' 
}
*/